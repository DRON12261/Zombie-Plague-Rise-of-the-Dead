/*================================================================================
	
	----------------------------------
	-*- [ZP ROTD] Class: Human: Runner -*-
	----------------------------------
	
	This plugin is part of Zombie Plague Mod and is distributed under the
	terms of the GNU General Public License. Check ZP_ReadMe.txt for details.
	
================================================================================*/

#include <amxmodx>
#include <fakemeta>
#include <cstrike>
#include <hamsandwich>
#include <cs_ham_bots_api>
#include <engine>

#include <zp50_core>
#include <zp50_class_survivor>
#include <zp50_class_sniper>
#include <zp50_class_human>

#define TASK_BOT 1200
#define ID_TASK_BOT (taskid - TASK_BOT)

#define MAXPLAYERS 32

// Classic Human Attributes
new const humanclass5_name[] = "Runner"
new const humanclass5_info[] = "GottaGoFast"
new const humanclass5_models[][] = { "arctic" , "guerilla" , "leet" , "terror" , "gign" , "gsg9" , "sas" , "urban" }
const humanclass5_health = 50
const humanclass5_armor = 0
const Float:humanclass5_speed = 2.0
const Float:humanclass5_gravity = 0.5

new g_HumanRunner

const XO_CWEAPONBOX = 4;
new const m_rgpPlayerItems_CWeaponBox[6] = {34, 35, ...}

const g_RestCountRunner = 18
new g_RestIdsRunner[g_RestCountRunner] = { CSW_GALIL, CSW_FAMAS, CSW_M4A1, CSW_AK47, CSW_SG552, CSW_AUG, CSW_M3, CSW_XM1014, CSW_M249, CSW_AWP, CSW_SG550, CSW_G3SG1, CSW_SCOUT, CSW_MAC10, CSW_UMP45, CSW_MP5NAVY, CSW_TMP, CSW_P90 }

new Float:g_LeapLastTimeRunner[MAXPLAYERS+1]

new Float:g_CooldownRunner = 2.0
new g_ForceRunner = 700
new Float:g_HeightRunner = 200.0

public plugin_precache()
{
	register_plugin("[ZP ROTD] Class: Human: Runner", ZP_VERSION_STRING, "ZP Dev Team + DRON12261")
	
	RegisterHam(Ham_Touch, "weaponbox", "fw_TouchWeapon")
	
	RegisterHam(Ham_TakeDamage, "player", "fw_TakeDamage")
	RegisterHamBots(Ham_TakeDamage, "fw_TakeDamage")
	
	RegisterHam(Ham_Item_AddToPlayer, "weapon_galil", "fw_ItemAddPost", true)
	RegisterHam(Ham_Item_AddToPlayer, "weapon_famas", "fw_ItemAddPost", true)
	RegisterHam(Ham_Item_AddToPlayer, "weapon_m4a1", "fw_ItemAddPost", true)
	RegisterHam(Ham_Item_AddToPlayer, "weapon_ak47", "fw_ItemAddPost", true)
	RegisterHam(Ham_Item_AddToPlayer, "weapon_sg552", "fw_ItemAddPost", true)
	RegisterHam(Ham_Item_AddToPlayer, "weapon_aug", "fw_ItemAddPost", true)
	RegisterHam(Ham_Item_AddToPlayer, "weapon_m3", "fw_ItemAddPost", true)
	RegisterHam(Ham_Item_AddToPlayer, "weapon_xm1014", "fw_ItemAddPost", true)
	RegisterHam(Ham_Item_AddToPlayer, "weapon_m249", "fw_ItemAddPost", true)
	RegisterHam(Ham_Item_AddToPlayer, "weapon_awp", "fw_ItemAddPost", true)
	RegisterHam(Ham_Item_AddToPlayer, "weapon_sg550", "fw_ItemAddPost", true)
	RegisterHam(Ham_Item_AddToPlayer, "weapon_g3sg1", "fw_ItemAddPost", true)
	RegisterHam(Ham_Item_AddToPlayer, "weapon_scout", "fw_ItemAddPost", true)
	RegisterHam(Ham_Item_AddToPlayer, "weapon_mac10", "fw_ItemAddPost", true)
	RegisterHam(Ham_Item_AddToPlayer, "weapon_ump45", "fw_ItemAddPost", true)
	RegisterHam(Ham_Item_AddToPlayer, "weapon_mp5navy", "fw_ItemAddPost", true)
	RegisterHam(Ham_Item_AddToPlayer, "weapon_tmp", "fw_ItemAddPost", true)
	RegisterHam(Ham_Item_AddToPlayer, "weapon_p90", "fw_ItemAddPost", true)
	
	register_forward(FM_PlayerPreThink, "fw_PlayerPreThink")
	
	g_HumanRunner = zp_class_human_register(humanclass5_name, humanclass5_info, humanclass5_health, humanclass5_armor, humanclass5_speed, humanclass5_gravity)
	new index
	for (index = 0; index < sizeof humanclass5_models; index++)
		zp_class_human_register_model(g_HumanRunner, humanclass5_models[index])
}

public client_connect(id)
{
	if (is_user_bot(id))
	{
		set_task(0.1, "BotsHandle", id+TASK_BOT, _, _, "b");
	}
}

public BotsHandle(taskid)
{
	if (!is_user_alive(ID_TASK_BOT) || !is_user_connected(ID_TASK_BOT) || zp_class_human_get_current(ID_TASK_BOT) != g_HumanRunner || zp_class_sniper_get(ID_TASK_BOT) || zp_class_survivor_get(ID_TASK_BOT))
	{
		return
	}
	
	if (!CanUserPickupWeapon(get_user_weapon(ID_TASK_BOT)))
	{
		engclient_cmd(ID_TASK_BOT, "drop")
	}
}

public client_disconnected(id)
{	
	if (is_user_bot(id))
	{
		remove_task(id+TASK_BOT)
	}
}

// Get entity's speed (from fakemeta_util)
stock fm_get_speed(entity)
{
	static Float:velocity[3]
	pev(entity, pev_velocity, velocity)
	
	return floatround(vector_length(velocity));
}

public fw_PlayerPreThink(id)
{
	if (!is_user_alive(id) || zp_class_human_get_current(id) != g_HumanRunner || zp_core_is_zombie(id) || zp_class_sniper_get(id) || zp_class_survivor_get(id))
	{
		return
	}
		
	static Float:current_time
	current_time = get_gametime()
	
	// Cooldown not over yet
	if (current_time - g_LeapLastTimeRunner[id] < g_CooldownRunner)
		return;
	
	// Not doing a longjump (don't perform check for bots, they leap automatically)
	if (!is_user_bot(id) && !(pev(id, pev_button) & (IN_JUMP | IN_DUCK) == (IN_JUMP | IN_DUCK)))
		return;
	
	// Not on ground or not enough speed
	if (!(pev(id, pev_flags) & FL_ONGROUND) || fm_get_speed(id) < 80)
		return;
	
	static Float:velocity[3]
	
	// Make velocity vector
	velocity_by_aim(id, g_ForceRunner, velocity)
	
	// Set custom height
	velocity[2] = g_HeightRunner
	
	// Apply the new velocity
	set_pev(id, pev_velocity, velocity)
	
	// Update last leap time
	g_LeapLastTimeRunner[id] = current_time
}

cs_get_weaponbox_type( iWeaponBox ) // assuming weaponbox contain only 1 weapon
{
    new iWeapon
    for(new i=1; i<=5; i++)
    {
        iWeapon = get_pdata_cbase(iWeaponBox, m_rgpPlayerItems_CWeaponBox[i], XO_CWEAPONBOX)
        if( iWeapon > 0 )
        {
            return cs_get_weapon_id(iWeapon)
        }
    }
    return 0
}

bool:CanUserPickupWeapon(weapon)
{
	for (new i = 0; i < g_RestCountRunner; i++)
	{
		if(weapon == g_RestIdsRunner[i])
		{
			return false
		}
	}
	return true
}

public fw_TouchWeapon(weapon, player)
{
	if (!is_user_alive(player) || zp_class_human_get_current(player) != g_HumanRunner)
	{
		return HAM_IGNORED
	}
	
	if(!CanUserPickupWeapon(cs_get_weaponbox_type(weapon)))
			return HAM_SUPERCEDE
	
	return HAM_IGNORED
}

public fw_ItemAddPost(weapon, player)
{	
	if (!is_user_alive(player) || zp_class_human_get_current(player) != g_HumanRunner)
	{
		return HAM_IGNORED
	}
	
	client_cmd(player, "drop")
	return HAM_HANDLED
}

public fw_TakeDamage(victim, inflictor, attacker, Float:damage, damage_type)
{
	if (victim == attacker || !is_user_alive(attacker) || zp_class_human_get_current(attacker) != g_HumanRunner || zp_core_is_zombie(attacker) || zp_class_sniper_get(attacker) || zp_class_survivor_get(attacker))
	{
		return HAM_IGNORED
	}
	
	if (inflictor == attacker)
	{
		new plrWeapId, plrClip, plrAmmo    
		plrWeapId = get_user_weapon(attacker, plrClip, plrAmmo)
		
		if (plrWeapId == CSW_P228 || CSW_ELITE || CSW_FIVESEVEN || CSW_USP || CSW_GLOCK18 || CSW_DEAGLE) 
		{
			SetHamParamFloat(4, damage * 10.5)
		}
		else if (plrWeapId == CSW_KNIFE) 
		{
			SetHamParamFloat(4, damage * 3.0)
		}
	}
	return HAM_HANDLED
}
